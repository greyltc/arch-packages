# bash <(curl -sL https://gist.github.com/greyltc/8a93d417a052e00372984ff8ec224703/raw) "https://raw.githubusercontent.com/greyltc/arch-packages/main/draftsight/{PKGBUILD,draftsight.install,draftsight.sh}" -i
pkgname=draftsight
pkgver=2019SP3
pkgrel=3
pkgdesc="Freeware CAD software for DWG/DXF files."
arch=('x86_64')
url="https://web.archive.org/web/20191028094853/https://www.3ds.com/products-services/draftsight-cad-software/download-draftsight/"
options=('!strip')
license=('custom')
source=(
	"${pkgname}-${pkgver}.rpm"::"https://web.archive.org/web/20200602160014/http://dl-ak.solidworks.com/nonsecure/draftsight/2019SP3/draftSight.rpm"
        "draftsight.sh"
)
sha256sums=('fd0567969da3b011d463e105e7b25a957646a7fbe7bda32eb08b18098886b08b'
            '4551ace36fe0d7639e94bdd23bcafcc72b7ddd680c2a680e01ca3f74d02ca1d2')
install=draftsight.install

prepare() {
  # use our launcher in the .desktop file
  sed -i 's|^Exec="/opt/dassault-systemes/DraftSight/Linux/DraftSight"|Exec=draftsight|g' opt/dassault-systemes/DraftSight/Resources/dassault-systemes_draftsight.desktop
  
  # fix mime type spec. in the .desktop file
  sed -i 's,application-vnd,application/vnd,g' opt/dassault-systemes/DraftSight/Resources/dassault-systemes_draftsight.desktop
  sed -i 's,^MimeType.*,&;,' opt/dassault-systemes/DraftSight/Resources/dassault-systemes_draftsight.desktop
}

package() {
	depends=(
		'alsa-lib'
		'desktop-file-utils'
		'fontconfig'
		'gcc-libs'
		'glib2'
		'gtk2'
		'libfaketime'
		'libcups'
		'libgl'
		'pcre'
		'libice'
		'libmariadbclient'
		'libmng'
		'libpng12'
		'libsm'
		'libx11'
		'libxext'
		'libxrender'
		'libxslt'
		'postgresql-libs'
		'qt5-base'
		'qt5-x11extras'
		'zlib'
		'firejail'
	)

	# the program
	cp -a opt "${pkgdir}"

	# our launcher
	install -Dm755 draftsight.sh "${pkgdir}/usr/bin/${pkgname}"

	# the EULA
	install -Dm644 opt/dassault-systemes/DraftSight/Eula/english/eula.htm "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.html

	# some icons
	for size in "16x16" "32x32" "48x48" "64x64" "128x128"; do
		install -Dm644 opt/dassault-systemes/DraftSight/Resources/pixmaps/${size}/program.png "${pkgdir}/usr/share/icons/hicolor/${size}/apps/dassault-systemes.draftsight.png"
		install -Dm644 opt/dassault-systemes/DraftSight/Resources/pixmaps/${size}/file-dwg.png "${pkgdir}/usr/share/icons/hicolor/${size}/mimetypes/file-dwg.png"
		install -Dm644 opt/dassault-systemes/DraftSight/Resources/pixmaps/${size}/file-dxf.png "${pkgdir}/usr/share/icons/hicolor/${size}/mimetypes/file-dxf.png"
		install -Dm644 opt/dassault-systemes/DraftSight/Resources/pixmaps/${size}/file-dwt.png "${pkgdir}/usr/share/icons/hicolor/${size}/mimetypes/file-dwt.png"
	done

	# mime stuff
	install -Dm644 opt/dassault-systemes/DraftSight/Resources/dassault-systemes_draftsight-dwg.xml "${pkgdir}/usr/share/mime/application/vnd.dassault-systemes_draftsight-dwg.xml"
	install -Dm644 opt/dassault-systemes/DraftSight/Resources/dassault-systemes_draftsight-dxf.xml "${pkgdir}/usr/share/mime/application/vnd.dassault-systemes_draftsight-dxf.xml"
	install -Dm644 opt/dassault-systemes/DraftSight/Resources/dassault-systemes_draftsight-dwt.xml "${pkgdir}/usr/share/mime/application/vnd.dassault-systemes_draftsight-dwt.xml"

	# .desktop file
	install -Dm644 opt/dassault-systemes/DraftSight/Resources/dassault-systemes_draftsight.desktop "${pkgdir}/usr/share/applications/dassault-systemes.draftsight.desktop"
}
